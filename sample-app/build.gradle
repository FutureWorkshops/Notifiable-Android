/*
 * Copyright Â©  2018 - 2019 FutureWorkshops. All rights reserved.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: '../android_commons.gradle'
apply from: '../android_dependencies.gradle'

android {

    defaultConfig {
        applicationId "com.futureworkshops.notifiable.sample"
        versionCode Integer.parseInt(project.property('project.buildnumber'))
        versionName project.property('project.buildversion')
        multiDexEnabled true
    }

    buildTypes {
        debug {

            if (file("../dev_credentials.properties").exists()) {
                def props = new Properties()
                file("../dev_credentials.properties").withInputStream {
                    props.load(it)
                }

                buildConfigField "String", "NOTIFIABLE_SERVER", props.getProperty('dev.notifiable.server')
                buildConfigField "String", "NOTIFIABLE_CLIENT_ID", props.getProperty('dev.notifiable.client')
                buildConfigField "String", "NOTIFIABLE_CLIENT_SECRET", props.getProperty('dev.notifiable.secret')

            } else {
                buildConfigField "String", "NOTIFIABLE_SERVER", project.property('notifiable.server')
                buildConfigField "String", "NOTIFIABLE_CLIENT_ID", project.property('notifiable.client')
                buildConfigField "String", "NOTIFIABLE_CLIENT_SECRET", project.property('notifiable.secret')
            }
        }

        release {
            buildConfigField "String", "NOTIFIABLE_SERVER", project.property('notifiable.server')
            buildConfigField "String", "NOTIFIABLE_CLIENT_ID", project.property('notifiable.client')
            buildConfigField "String", "NOTIFIABLE_CLIENT_SECRET", project.property('notifiable.secret')

            minifyEnabled false
        }
    }


    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "NotifiableSample-${variant.name}.apk"
        }
    }

}

dependencies {

//    implementation project(':library')
    implementation project(':notifiable-rx')

    implementation Libraries.kotlinStdLib
    implementation Libraries.ktx

    implementation Libraries.rxjava
    implementation Libraries.rxAndroid

    implementation Libraries.timber

    implementation Libraries.lifecycleRuntime
    implementation Libraries.lifecycleExtensions
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    kapt Libraries.lifecycleCompiler


    // UI
    implementation SupportLibraries.appCompatX
    implementation SupportLibraries.annotationX
    implementation SupportLibraries.material
    implementation SupportLibraries.constraintLayout

    // dagger
    implementation Libraries.dagger
    kapt Libraries.daggerCompiler


    implementation Libraries.daggerAndroid
    implementation Libraries.daggerAndroidSupport
    kapt Libraries.daggerAndroidProcessor

    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.0.0'

}

apply plugin: 'com.google.gms.google-services'
